plugins {
    id 'java'
    id 'jacoco'
    // Jitpack.io's publishing service needs the task publishToMavenLocal
    id 'maven-publish'
}

final var RELEASE_FOR = JavaVersion.VERSION_17
if (!JavaVersion.current().isCompatibleWith(RELEASE_FOR)) {
    throw new Exception(
         "Java ${JavaVersion.current()} is not supported. " +
         "Please use at least version ${RELEASE_FOR}.");
}

sourceSets {
    // Move "unit" test locations from default '/test' to '/test/small'
    test {
        // Syntax 1:
        java.srcDirs = ['src/test/small/java']
        // ...and syntax 2 (because confusion is so much fun):
        java.destinationDirectory.set file("$buildDir/classes/java/test/small")
        resources.srcDirs = ['src/test/small/resources']
        output.resourcesDir = file("$buildDir/resources/test/small")
    }
    
    // Define medium tests in '/test/medium'
    mediumTest {
        java.srcDirs = ['src/test/medium/java']
        java.destinationDirectory.set file("$buildDir/classes/java/test/medium")
        resources.srcDirs = ['src/test/medium/resources']
        output.resourcesDir = file("$buildDir/resources/test/medium")
        // Medium can also pull in utils from small
        // (e.g. alpha.nomagichttp.testutil.Logging)
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
    
    // Define large tests in '/test/large'
    largeTest {
        java.srcDirs = ['src/test/large/java']
        java.destinationDirectory.set file("$buildDir/classes/java/test/large")
        resources.srcDirs = ['src/test/large/resources']
        output.resourcesDir = file("$buildDir/resources/test/large")
        // Large can also pull in utils from medium (and small)
        // (e.g. alpha.nomagichttp.testutil.AbstractRealTest)
        compileClasspath += main.output + mediumTest.output + test.output
        runtimeClasspath += main.output + mediumTest.output + test.output
    }
}

configurations {
    // All small test dependencies are also medium+large test dependencies
    // (e.g. @org.junit.jupiter.params.ParameterizedTest)
    mediumTestImplementation.extendsFrom testImplementation
    mediumTestRuntimeOnly.extendsFrom testRuntimeOnly
    largeTestImplementation.extendsFrom testImplementation
    largeTestRuntimeOnly.extendsFrom testRuntimeOnly
    
    // All medium test dependencies are also large test dependencies
    // (e.g. HTTP client implementations)
    largeTestImplementation.extendsFrom mediumTestImplementation
    largeTestRuntimeOnly.extendsFrom mediumTestRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    
    // HTTP client implementations used by HttpClientFacade
    mediumTestImplementation 'com.squareup.okhttp3:okhttp:4.9.2'
    mediumTestImplementation 'org.apache.httpcomponents.client5:httpclient5:5.1.1'
    mediumTestImplementation 'org.eclipse.jetty:jetty-client:11.0.6'
    mediumTestImplementation 'io.projectreactor.netty:reactor-netty-http:1.0.13'
}

// To be bumped by task in future or perhaps not even set in this file
version = '0.5-SNAPSHOT'

// By default, all compiler- and JavaDoc warnings are errors.
// Unless environment variable HACKING=true.
var Werror = !(System.env.HACKING ?: false).toBoolean()

wrapper {
    gradleVersion = '7.3'
    distributionType = Wrapper.DistributionType.ALL
}

// Ensure backwards compatibility for Java 11
// https://stackoverflow.com/a/43103038/1268003
// https://docs.gradle.org/6.6/release-notes.html#javacompile-release
compileJava {
    options.release = RELEASE_FOR.getMajorVersion().toInteger()
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll([
        '-Xlint',                  // Enable all warnings
        '-Xdoclint:all/protected', // Also check JavaDoc (protected and higher)
        '-Xdiags:verbose'])        // Talk more
    
    // Fail on any warning
    if (Werror) options.compilerArgs.add '-Werror'
}

test {
    useJUnitPlatform()
}

task mediumTest(type: Test) {
    description = 'Runs the medium tests.'
    group = 'verification'
    
    useJUnitPlatform()
    
    testClassesDirs = sourceSets.mediumTest.output.classesDirs
    classpath = sourceSets.mediumTest.runtimeClasspath
    shouldRunAfter test
}

task largeTest(type: Test) {
    description = 'Runs the large tests.'
    group = 'verification'
    
    useJUnitPlatform()
    
    testClassesDirs = sourceSets.largeTest.output.classesDirs
    classpath = sourceSets.largeTest.runtimeClasspath
    shouldRunAfter mediumTest
}

// Tell Gradle the obvious: build task runs all tests
check.dependsOn tasks.withType(Test)

// Generate report after tests
tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // Use data file not just from test but also from medium+large
    getExecutionData().setFrom(
            fileTree(dir: "$buildDir/jacoco", includes: ['*.exec']))
}

javadoc {
    title = 'NoMagicHTTP ' + version + ' API'
    // default description was "Generates Javadoc API documentation for the main source code."
    description = 'Generates Javadoc API documentation (public symbols from main source set).'
}

// Build JavaDoc HTML for developers
task javadocAll(type: Javadoc) {
    title = javadoc.title + ' (for library developers)'
    description = 'Generates all Javadoc API documentation (public+private symbols and test classes).'
    group = 'documentation'
    
    source = [
        sourceSets.main.allJava,
        sourceSets.test.allJava,
        sourceSets.mediumTest.allJava,
        sourceSets.largeTest.allJava ]
    
    classpath = sourceSets.mediumTest.compileClasspath +
                sourceSets.largeTest.compileClasspath
    
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file("$buildDir/docs/javadoc-all")
}

tasks.withType(Javadoc) {
    // Add support for modern JavaDoc tags
    // https://openjdk.java.net/jeps/8068562
    options.tags(
        'apiNote:a:API Note:',
        'implSpec:a:Implementation Requirements:',
        'implNote:a:Implementation Note:')
    
    // Fail on any warning
    // https://stackoverflow.com/a/49544352/1268003
    // https://github.com/gradle/gradle/issues/2354#issuecomment-364857181
    if (Werror) {
        var opt = JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_15) ?
            "Werror" : "Xwerror";
        
        options.addBooleanOption(opt, true)
    } 
    
    // No warnings for missed comments (can't disable only for private symbols)
    options.addBooleanOption('Xdoclint:all,-missing', true)
    
    // Text to be injected as overview
    options.overview = 'src/main/java/overview.html'
    
    // Link to JDK JavaDoc
    var v = compileJava.options.release.get()
    options.links("https://docs.oracle.com/en/java/javase/$v/docs/api")
}

// Add tasks "javadocJar" and "sourcesJar" (for IDE:s and other tooling)
java {
    withJavadocJar() // .jar contains HTML from the javadoc task, not javadocAll
    withSourcesJar() // contains .java source code files; no .class files or HTML
}

// withJavadocJar() added the javadoc task as an assemble task dependency,
// do the same for javadocAll (gradle build will fail if any doc task fails)
assemble.dependsOn javadocAll

// For jitpack.io, define a publication
publishing.publications {
    thisNameMakesNoDifference(MavenPublication) {
        from components.java
        pom.licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
    }
}