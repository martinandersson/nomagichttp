plugins {
    // TODO: Use explicit versions of each (just as we do wrapper)
    id 'java'
    id 'jacoco'
}

sourceSets {
    test {
        // Move unit test locations from default '/test' to 'test/unit'
        java.srcDirs = ['src/test/unit/java']
        java.outputDir = file("$buildDir/classes/java/test/unit")
        resources.srcDirs = ['src/test/unit/resources']
        output.resourcesDir = file("$buildDir/resources/test/unit")
    }
    
    largeTest {
        java.srcDirs = ['src/test/large/java']
        java.outputDir = file("$buildDir/classes/java/test/large")
        resources.srcDirs = ['src/test/large/resources']
        output.resourcesDir = file("$buildDir/resources/test/large")
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    // All unit test dependencies are also large test dependencies
    largeTestImplementation.extendsFrom testImplementation
    largeTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    jcenter()
}

dependencies {
    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    
    testImplementation 'org.mockito:mockito-core:3.4.4'
    
    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    
    largeTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.1'
}

compileJava {
    // TODO: Figure out this: https://stackoverflow.com/a/22326989
    //sourceCompatibility = JavaVersion.VERSION_11
    //targetCompatibility = JavaVersion.VERSION_11
}

// https://stackoverflow.com/a/22326989
// https://stackoverflow.com/a/43103038
// https://github.com/gradle/gradle/issues/2510

if (JavaVersion.current() < JavaVersion.VERSION_11) {
    throw new GradleException("This project requires at least Java 11, but it's running on " + JavaVersion.current() + ".");
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(['--release', '11', '-Xlint:unchecked', '-Xdiags:verbose'])
}

test {
    useJUnitPlatform()
}

task largeTest(type: Test) {
    description = 'Runs the large tests.'
    group = 'verification'
    
    useJUnitPlatform()
    
    testClassesDirs = sourceSets.largeTest.output.classesDirs
    classpath = sourceSets.largeTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn largeTest

// Generate report after tests
tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // Use data file not just from test but also largeTest
    getExecutionData().setFrom(fileTree(dir: "$buildDir/jacoco", includes: ['*.exec']))
}