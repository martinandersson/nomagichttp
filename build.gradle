plugins {
    id 'java'
    id 'jacoco'
    // Jitpack.io's publishing service needs the task publishToMavenLocal
    id 'maven-publish'
}

final var RELEASE_FOR = JavaVersion.VERSION_19
if (!JavaVersion.current().isCompatibleWith(RELEASE_FOR)) {
    throw new Exception(
         "Java ${JavaVersion.current()} is not supported. " +
         "Please use at least version ${RELEASE_FOR}.");
}

sourceSets {
    // Move "unit" test locations from default '/test' to '/test/small'
    test {
        // Syntax 1:
        java.srcDirs = ['src/test/small/java']
        // ...and syntax 2 (because confusion is so much fun):
        java.destinationDirectory.set file("$buildDir/classes/java/test/small")
        resources.srcDirs = ['src/test/small/resources']
        output.resourcesDir = file("$buildDir/resources/test/small")
    }
    
    // Define medium tests in '/test/medium'
    mediumTest {
        java.srcDirs = ['src/test/medium/java']
        java.destinationDirectory.set file("$buildDir/classes/java/test/medium")
        resources.srcDirs = ['src/test/medium/resources']
        output.resourcesDir = file("$buildDir/resources/test/medium")
        // Medium can also pull in utils from small
        // (e.g. alpha.nomagichttp.testutil.Logging)
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
    
    // Define large tests in '/test/large'
    largeTest {
        java.srcDirs = ['src/test/large/java']
        java.destinationDirectory.set file("$buildDir/classes/java/test/large")
        resources.srcDirs = ['src/test/large/resources']
        output.resourcesDir = file("$buildDir/resources/test/large")
        // Large can also pull in utils from small and medium
        // (e.g. alpha.nomagichttp.testutil.AbstractRealTest)
        compileClasspath += main.output + test.output + mediumTest.output
        runtimeClasspath += main.output + test.output + mediumTest.output
    }
}

configurations {
    // All small test dependencies are also medium+large test dependencies
    // (e.g. @org.junit.jupiter.api.Test)
    mediumTestImplementation.extendsFrom testImplementation
    mediumTestRuntimeOnly.extendsFrom testRuntimeOnly
    largeTestImplementation.extendsFrom testImplementation
    largeTestRuntimeOnly.extendsFrom testRuntimeOnly
    
    // All medium test dependencies are also large test dependencies
    // (e.g. alpha.nomagichttp.testutil.HttpClientFacade)
    largeTestImplementation.extendsFrom mediumTestImplementation
    largeTestRuntimeOnly.extendsFrom mediumTestRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    
    testImplementation 'org.assertj:assertj-core:3.24.1'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    
    // HTTP client implementations used by HttpClientFacade
    mediumTestImplementation 'com.squareup.okhttp3:okhttp:4.10.0'
    mediumTestImplementation 'org.apache.httpcomponents.client5:httpclient5:5.1.1'
    mediumTestImplementation 'org.eclipse.jetty:jetty-client:11.0.13'
    mediumTestImplementation 'io.projectreactor.netty:reactor-netty-http:1.1.2'
}

// To be bumped by task in future or perhaps not even set in this file
version = '0.5-SNAPSHOT'

// By default, all compiler- and JavaDoc warnings are errors.
// Unless environment variable HACKING=true.
var Werror = !(System.env.HACKING ?: false).toBoolean()



// ---------------------   TEMP   ------------------------
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(
        // Fixes "DefaultServer.java:130: error: patterns in switch statements are a preview feature and are disabled by default"
        '--enable-preview',
        // Fixes "HttpServer.java:26: error: package jdk.incubator.concurrent is not visible"
        '--add-modules', 'jdk.incubator.concurrent')
}
tasks.withType(Test).configureEach {
    jvmArgs(
        // Fixes "java.lang.UnsupportedClassVersionError: Preview features are not enabled..."
        '--enable-preview',
        // java.lang.ClassNotFoundException: jdk.incubator.concurrent.StructuredTaskScope
        '--add-modules', 'jdk.incubator.concurrent')
}
// Preview features and incubator modules produce warnings, so suppress treating warning as errors:
Werror = false;
tasks.withType(Javadoc).configureEach {
    options {
        addBooleanOption('-enable-preview', true)
        addStringOption('-release', "$RELEASE_FOR")
        addStringOption('-add-modules', 'jdk.incubator.concurrent')
    }
}
// TODO: Will probably have to add the JVM flags to withType(JavaExec) as well
// -------------------------------------------------------



tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(
        // Enable all warnings, minus try-with-resources
        // https://stackoverflow.com/a/73861648/1268003
        '-Xlint:all,-try',
        // Also check JavaDoc (protected and higher)
        '-Xdoclint:all/protected',
        // Talk more
        '-Xdiags:verbose',
        // https://stackoverflow.com/a/43103038/1268003
        // https://docs.gradle.org/6.6/release-notes.html#javacompile-release
        '--release', RELEASE_FOR)
    
    // Fail on any warning
    if (Werror) options.compilerArgs.add '-Werror'
}

// All tests will use JUnit and generate coverage report
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.register('mediumTest', Test) {
    description = 'Runs the medium tests.'
    group = 'verification'
    
    testClassesDirs = sourceSets.mediumTest.output.classesDirs
    classpath = sourceSets.mediumTest.runtimeClasspath
    shouldRunAfter test
}

tasks.register('largeTest', Test) {
    description = 'Runs the large tests.'
    group = 'verification'
    
    testClassesDirs = sourceSets.largeTest.output.classesDirs
    classpath = sourceSets.largeTest.runtimeClasspath
    shouldRunAfter mediumTest
}

// By default, "gradle build" includes only small tests, add medium and large
check.dependsOn tasks.withType(Test)

jacocoTestReport {
    // Use data file not just from test but also from medium+large
    getExecutionData().setFrom(
            fileTree(dir: "$buildDir/jacoco", includes: ['*.exec']))
}

javadoc {
    title = 'NoMagicHTTP ' + version + ' API'
    // default description was "Generates Javadoc API documentation for the main source code."
    description = 'Generates Javadoc API documentation (public symbols from main source set).'
}

// Build JavaDoc HTML for developers
tasks.register('javadocAll', Javadoc) {
    title = javadoc.title + ' (for library developers)'
    description = 'Generates all Javadoc API documentation (public+private symbols and test classes).'
    group = 'documentation'
    
    source = [
        sourceSets.main.allJava,
        sourceSets.test.allJava,
        sourceSets.mediumTest.allJava,
        sourceSets.largeTest.allJava ]
    
    classpath = sourceSets.mediumTest.compileClasspath +
                sourceSets.largeTest.compileClasspath
    
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file("$buildDir/docs/javadoc-all")
}

tasks.withType(Javadoc).configureEach {
    // Add support for modern JavaDoc tags
    // https://openjdk.java.net/jeps/8068562
    options.tags(
        'apiNote:a:API Note:',
        'implSpec:a:Implementation Requirements:',
        'implNote:a:Implementation Note:')
    
    // Fail on any warning
    // https://stackoverflow.com/a/49544352/1268003
    // https://github.com/gradle/gradle/issues/2354#issuecomment-364857181
    if (Werror) {
        var opt = JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_15) ?
            "Werror" : "Xwerror";
        
        options.addBooleanOption(opt, true)
    }
    
    // No warnings for missed comments (can't disable only for private symbols)
    options.addBooleanOption('Xdoclint:all,-missing', true)
    
    // Text to be injected as overview
    options.overview = 'src/main/java/overview.html'
    
    // Link to JDK JavaDoc
    options.links("https://docs.oracle.com/en/java/javase/$RELEASE_FOR/docs/api")
}

// Add tasks "javadocJar" and "sourcesJar" (for IDE:s and other tooling)
java {
    withJavadocJar() // .jar contains HTML from the javadoc task, not javadocAll
    withSourcesJar() // contains .java source code files; no .class files or HTML
}

// withJavadocJar() added the javadoc task as an assemble task dependency,
// do the same for javadocAll (gradle build will fail if any doc task fails)
assemble.dependsOn javadocAll

// For jitpack.io, define a publication
publishing.publications {
    thisNameMakesNoDifference(MavenPublication) {
        from components.java
        pom.licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
    }
}

// TODO: cleanTest should also remove Jacoco files